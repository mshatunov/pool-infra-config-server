buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.6'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'

group = 'com.mshatunov.pool.infra'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}


ext {
    springCloudVersion = 'Finchley.M9'
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-config-server')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    dockerJava 'com.github.docker-java:docker-java:3.1.0-rc-2'
    dockerJava 'cglib:cglib:3.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

import com.bmuschko.gradle.docker.DockerRegistryCredentials
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task dockerfile(type: Dockerfile) {
    from 'frolvlad/alpine-oraclejdk8:slim'
    addFile "${bootJar.archiveName}", '.'
    entryPoint 'java', '-jar', "${bootJar.archiveName}"
}

task copyJar(type: Copy, dependsOn: bootJar) {
    from bootJar.archivePath
    into dockerfile.destFile.parentFile
}

task buildImage(type: DockerBuildImage,dependsOn: [build, dockerfile, copyJar, check]) {
    inputDir dockerfile.destFile.parentFile
    tag = 'mshatunov/pool-infra-config-server:latest'
}

task pushImage(type: DockerPushImage, dependsOn: buildImage) {
    imageName = buildImage.tag
    def dockerRegistryCredentials = new DockerRegistryCredentials()
    dockerRegistryCredentials.username = "${dockerUser}"
    dockerRegistryCredentials.password = "${dockerPassword}"
    registryCredentials = dockerRegistryCredentials
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
